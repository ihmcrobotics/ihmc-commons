//buildscript {
//   repositories {
//      mavenLocal()
//      jcenter()
//   }
//
//   dependencies {
//      classpath "us.ihmc.gradle:ihmc-build:0.0.24"
//   }
//}

plugins {
   id 'java'
   id 'eclipse'
   id 'idea'
   id 'maven-publish'
   id 'us.ihmc.gradle.ihmc-build' version '0.0.24'
}

//apply plugin: 'us.ihmc.gradle.ihmc-build'

sourceCompatibility = 1.8
group = 'us.ihmc'
version = '0.11.2'

if (publishMode == 'SNAPSHOT')
{
   version = ihmc.getSnapshotVersion(version, bambooBuildNumber)
}

project.ext.vcsUrl = 'https://github.com/ihmcrobotics/ihmc-commons'
project.ext.licenseURL = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
project.ext.licenseName = 'Apache License, Version 2.0'
project.ext.bintrayLicenseName = 'Apache-2.0'
project.ext.testingPublicationName = 'ihmc-commons-testing'

jar {
   manifest {
      attributes(
            'Created-By': 'IHMC Gradle Build Script',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'IHMC',

            'Bundle-Name': project.name,
            'Bundle-Version': project.version,
            'Bundle-License': project.ext.licenseURL,
            'Bundle-Vendor': 'IHMC')
   }
}

project.sourceSets {
   main {
      java {
         srcDirs = ['src']
      }
   }
   testing {
      java {
         srcDirs = ['testing']
      }
   }
   test {
      java {
         srcDirs = ['test']
      }

      resources {
         srcDirs = ['testResources']
      }
   }
}

repositories ihmc.setupCommonArtifactProxies()

repositories {
   mavenCentral()
   jcenter()
   mavenLocal()
}

dependencies {
   compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
   compile group: 'commons-io', name: 'commons-io', version: '2.4'

   testingCompile group: 'junit', name: 'junit', version: '4.11'
   testingCompile group: 'us.ihmc', name: 'ihmc-continuous-integration-framework', version: '0.9.4'
   testingCompile group: 'org.pitest', name: 'pitest-command-line', version: '1.1.9'
   testingCompile sourceSets.main.output
   testingCompile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
   testingCompile group: 'commons-io', name: 'commons-io', version: '2.4'

   testCompile group: 'junit', name: 'junit', version: '4.11'
   testCompile group: 'us.ihmc', name: 'ihmc-continuous-integration-framework', version: '0.9.4'
   testCompile group: 'org.pitest', name: 'pitest-command-line', version: '1.1.9'
   testCompile sourceSets.main.output
   testCompile sourceSets.testing.output
   testCompile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
   testCompile group: 'commons-io', name: 'commons-io', version: '2.4'
}

ihmc.declareArtifactoryOrBintray(project, publishMode)

ihmc.declarePublication(project, "Main", project.name, configurations.compile, sourceSets.main)
ihmc.declarePublication(project, "Testing", project.name + '-testing', configurations.testingCompile, sourceSets.testing, project.name)
ihmc.declarePublication(project, "Test", project.name + '-test', configurations.testCompile, sourceSets.test, project.name, project.name + '-testing')
