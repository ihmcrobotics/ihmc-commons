plugins {
   id "com.jfrog.bintray" version "1.7.3"
   id "com.jfrog.artifactory" version "4.4.18"
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
group = 'us.ihmc'
version = '0.11.0-alpha'

if (buildType == 'SNAPSHOT')
{
   version += '-' + bambooBuildNumber + '-SNAPSHOT'
}

project.ext.fullVersion = version
project.ext.vcsUrl = 'https://github.com/ihmcrobotics/ihmc-commons'
project.ext.licenseURL = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
project.ext.licenseName = 'Apache License, Version 2.0'
project.ext.bintrayLicenseName = 'Apache-2.0'
project.ext.mainPublicationName = 'ihmc-commons'
project.ext.testingPublicationName = 'ihmc-commons-testing'
println 'Publish target: ' + publishTarget
if (publishTarget == 'testing')
{
   project.ext.publicationName = project.ext.testingPublicationName
} else
{
   project.ext.publicationName = project.ext.mainPublicationName
}

println project.ext.publicationName

jar {
   manifest {
      attributes(
            'Created-By': 'IHMC Gradle Build Script',
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'IHMC',

            'Bundle-Name': project.ext.publicationName,
            'Bundle-Version': project.version,
            'Bundle-License': project.ext.licenseURL,
            'Bundle-Vendor': 'IHMC')
   }
}

project.sourceSets {
   if (publishTarget == 'testing')
   {
      main {
         java {
            srcDirs = ['testing']
         }
      }
      primary {
         java {
            srcDirs = ['src']
         }
      }
   } else if (publishTarget == 'main')
   {
      main {
         java {
            srcDirs = ['src']
         }
      }
   } else if (publishTarget == 'all')
   {
      main {
         java {
            srcDirs = ['src', 'testing']
         }
      }
      test {
         java {
            srcDirs = ['test']
         }

         resources {
            srcDirs = ['testResources']
         }
      }
   }
}

task sourceJar(type: Jar) {
   from sourceSets.main.allJava
}

task javaDocJar(type: Jar, dependsOn: javadoc) {
   from javadoc.destinationDir
}

publishing {
   publications {
      mavenJava(MavenPublication) {
         groupId 'us.ihmc'
         artifactId project.ext.publicationName
         version project.ext.fullVersion
         from components.java

         pom.withXml {
            asNode().children().last() + {
               resolveStrategy = DELEGATE_FIRST
               name project.name
               url project.ext.vcsUrl
               licenses {
                  license {
                     name project.ext.licenseName
                     url project.ext.licenseURL
                     distribution 'repo'
                  }
               }
            }
         }

         artifact sourceJar {
            classifier 'sources'
         }

         // Someday we'll get Javadoc working, but there's source
         // artifact javaDocJar {
         //    classifier 'javadoc'
         // }
      }
   }
}

//project.ext.buildInfo.build.name = project.ext.publicationName
//project.ext.buildInfo.build.number = bambooBuildNumber
//project.ext.buildInfo.build.version = version
//project.ext.buildInfo.build.url = 'https://bamboo.ihmc.us/browse/LIBS-IHMCCOMMONS-' + bambooBuildNumber
//project.ext.buildInfo.build.vcsUrl = project.ext.vcsUrl

//buildInfo {
//   buildNumber = bambooBuildNumber
//   url = 'https://bamboo.ihmc.us/browse/LIBS-IHMCCOMMONS-' + bambooBuildNumber
//   vcsUrl = project.ext.vcsUrl
//}

artifactory {
   contextUrl = 'https://artifactory.ihmc.us/artifactory'

   publish {
      repository {
         repoKey = 'snapshots'
         username = artifactoryUsername
         password = artifactoryPassword
      }
      defaults {
         publications('mavenJava')
      }
   }

   resolve {
      repository {
         repoKey = 'libs-releases'
      }
   }
}

bintray {
   user = bintray_user
   key = bintray_key

   dryRun = false
   publish = false

   publications = ['mavenJava']

   pkg {
      repo = 'maven-release'
      name = project.ext.publicationName
      userOrg = 'ihmcrobotics'
      desc = project.name

      websiteUrl = project.ext.vcsUrl
      issueTrackerUrl = project.ext.vcsUrl + '/issues'
      vcsUrl = project.ext.vcsUrl + '.git'

      licenses = [project.ext.bintrayLicenseName]
      labels = ['ihmc', 'java']
      publicDownloadNumbers = true

      version {
         name = project.ext.fullVersion
         desc = project.name + ' v' + project.ext.fullVersion
         released = new Date()
         vcsTag = 'v' + project.version
      }
   }
}

repositories {
   mavenCentral()
   jcenter()
   mavenLocal()
   maven {
      url 'http://dl.bintray.com/ihmcrobotics/maven-vendor'
   }
   maven {
      url 'http://dl.bintray.com/ihmcrobotics/maven-release'
   }
   maven {
      url 'http://clojars.org/repo/'
   }
   maven {
      url 'https://github.com/rosjava/rosjava_mvn_repo/raw/master'
   }
   maven {
      url 'https://oss.sonatype.org/content/repositories/snapshots'
   }
   maven {
      url 'https://artifactory.ihmc.us/artifactory/releases/'
   }
   maven {
      url 'https://artifactory.ihmc.us/artifactory/thirdparty/'
   }
}

dependencies {
   compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
   compile group: 'commons-io', name: 'commons-io', version: '2.4'

   if (publishTarget == 'testing')
   {
      compile group: 'junit', name: 'junit', version: '4.11'
      compile group: 'us.ihmc', name: 'ihmc-continuous-integration-framework', version: '0.9.4'
      compile group: 'org.pitest', name: 'pitest-command-line', version: '1.1.9'
      compile sourceSets.primary.output
      primaryCompile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
      primaryCompile group: 'commons-io', name: 'commons-io', version: '2.4'
   } else if (publishTarget == 'all')
   {
      compile group: 'org.pitest', name: 'pitest-command-line', version: '1.1.9'
      testCompile group: 'junit', name: 'junit', version: '4.11'
      testCompile group: 'us.ihmc', name: 'ihmc-continuous-integration-framework', version: '0.9.4'
      testCompile group: 'org.pitest', name: 'pitest-command-line', version: '1.1.9'
   }
}
